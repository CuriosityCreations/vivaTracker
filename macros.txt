SET(SEQUENCES_PATH ${CMAKE_SOURCE_DIR}/sequences/)

SET(vot2013  http://www.site.uottawa.ca/research/viva/datasets/tracking/vot2013.tar.gz)
SET(md5_vot2013  7467447b0d533efcb458ab106c66497a)

SET(vot2014  http://www.site.uottawa.ca/research/viva/datasets/tracking/vot2014.tar.gz)
SET(md5_vot2014  3161df3bfc62802d5f5a66a5d4af82b6)

SET(vot2015  http://www.site.uottawa.ca/research/viva/datasets/tracking/vot2015.tar.gz)
SET(md5_vot2015  2e8c33eed5dc08d8e4892fd9e4c1c2ed)

SET(vot2015tir  http://www.site.uottawa.ca/research/viva/datasets/tracking/vot2015tir.tar.gz)
SET(md5_vot2015tir  367c1f685d4a28a448f6c4bc2fcb3552)

MACRO(SUBDIRLIST result curdir)
  FILE(GLOB children RELATIVE ${curdir} ${curdir}/*)
  SET(dirlist "")
  FOREACH(child ${children})
    IF(IS_DIRECTORY ${curdir}/${child})
        LIST(APPEND dirlist ${child})
    ENDIF()
  ENDFOREACH()
  SET(${result} ${dirlist})
ENDMACRO()

MACRO(UNZIP file dest)
	FILE(MAKE_DIRECTORY ${dest})
	execute_process(
     COMMAND ${CMAKE_COMMAND} -E tar xzf ${file} 
     WORKING_DIRECTORY ${dest}
    )
ENDMACRO()

MACRO(CHECK_VOT_OPT url)
	GET_FILENAME_COMPONENT(name ${url} NAME_WE)
	GET_FILENAME_COMPONENT(filename ${url} NAME)
	IF(${download_${name}})
	
		SET (DATASET "${SEQUENCES_PATH}${filename}")
		SET (DATASET_FOLDER ${SEQUENCES_PATH}${name}/)
		SET (DATASET_MD5 ${md5_${name}})
		
		FILE(MAKE_DIRECTORY ${DATASET_FOLDER})
		IF ( NOT EXISTS  ${DATASET})
			 MESSAGE(STATUS "    downloading dataset: ${DATASET}")
		 	 FILE(DOWNLOAD ${url} ${DATASET} SHOW_PROGRESS EXPECTED_HASH MD5=${DATASET_MD5})
		ENDIF()
		IF ( EXISTS ${DATASET} AND NOT EXISTS  "${DATASET_FOLDER}list.txt")
			MESSAGE(STATUS "    unziping dataset: ${DATASET}")
			UNZIP(${DATASET} ${SEQUENCES_PATH})
		ENDIF()
	ENDIF()
ENDMACRO()

MACRO(CHECK_VOT_OPTS)
	SET(DATASETS ${vot2013} ${vot2014} ${vot2015} ${vot2015tir})
	FOREACH(url ${DATASETS})
		CHECK_VOT_OPT(${url})
	ENDFOREACH()
	FILE(WRITE ${CMAKE_SOURCE_DIR}/sequences.txt "${SEQUENCES_PATH}" )
ENDMACRO()
